// installer lib for the Mac version of the EEex loader
// author: CrevsDaak
// Time-stamp: </Users/nico/BG_modding/EEexMacLoader/EEex_MacOSX.tph, 2019-08-08 Thursday 20:52:07 nico>
// v1.0

/*
 * The variable
 *   EEex_osx_loader_based
 *
 * Can be redirected to anything else you'd like,
 * in case you feel like breaking things or
 * re-distributing EEex a la TobEx, which is fine
 * and I don't mind it as long as you don't break
 * things.
 *
 * On the other hand, the variable
 *   EEex_launch_path
 *
 * Shouldn't be changed without a good reason, since
 * users won't find their EEex.sh if you change it.
 *
 */
 
/* 
 * EEex_MacOSX.tph TO DO LIST:
 *   get this thing and the binaries to the main EEex repo
 *   check whether EEex is already installed
 *   automatically beautify EEex.sh
 */

AT_NOW ~pwd >.c7_cwd~

COPY ".c7_cwd" "."
  READ_ASCII 0x0 c7_game_wd (%SOURCE_SIZE% - 1)
BUT_ONLY

AT_NOW ~rm -f .c7_cwd~

OUTER_INNER_PATCH_SAVE c7_game_executable "%USER_DIRECTORY%" BEGIN
  REPLACE_TEXTUALLY EVALUATE_REGEXP ~^/Users/.*/Documents/\(.*\)$~ ~\1~
END

<<<<<<<< __inlined__c7__EEex.sh
#!/bin/sh
cd GAME_PATH;
DYLD_INSERT_LIBRARIES=EEexLoader.dylib GAME_EXEC; exit;
>>>>>>>>

OUTER_SPRINT c7_game_exe_path EVAL "%c7_game_executable%.app/Contents/MacOS"

ACTION_IF !VARIABLE_IS_SET EEex_launch_path BEGIN
  OUTER_SPRINT EEex_launch_path "."
END

COPY __inlined__c7__EEex.sh "%EEex_launch_path%/EEex.sh"
  REPLACE_TEXTUALLY "GAME_EXEC" ~"./%c7_game_executable%"~
  REPLACE_TEXTUALLY "GAME_PATH" ~"%c7_game_wd%/%c7_game_exe_path%/"~

AT_NOW ret ~chmod +x "%c7_game_exe_path%/EEex.sh"~

ACTION_IF ret > 0 BEGIN
  WARN ~WARNING: failed to mark EEex.sh as an executable file.~
END

ACTION_IF !VARIABLE_IS_SET EEex_osx_loader_based BEGIN
  OUTER_SPRINT EEex_osx_loader_based "EEex/osx"
END

COPY "%EEex_osx_loader_based%/EEexLoader.dylib" "%c7_game_exe_path%/"

PRINT ~EEex has been installed. Please launch the game via EEex.sh if you wish to make use of it.~
